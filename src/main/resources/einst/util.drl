import uk.ac.imperial.einst.Action
import uk.ac.imperial.einst.T

global uk.ac.imperial.einst.EInstSession einst

declare Action
	@role( event )
end

declare Logged
	action : Action
end

rule "Create T"
	when
	then
		insert(new T());
end

rule "Update T"
	salience 1000
	when
		Action($t : t)
		$tt : T(t < $t)
		not( exists( Action(t > $t) ))
	then
		modify($tt) {
			setT($t)
		}
end

rule "Log Actions"
	when
		$a : Action()
		not( Logged($a;) )
	then
		einst.logAction($a);
		insert(new Logged($a));
end
