import uk.ac.imperial.einst.micropay.Account
import uk.ac.imperial.einst.micropay.Invoice
import uk.ac.imperial.einst.micropay.Transfer

global org.apache.log4j.Logger logger

declare Paid
	invoice : Invoice
end

declare Cleared
	transfer : Transfer
end

query cleared(Transfer t)
	Cleared(t := transfer)
end

rule "Pay Invoice"
	when
		$i : Invoice($p : payer, $q : amount)
		$a : Account(holder == $p, $b : balance)
		not( Paid($i;) )
	then
		logger.info("Paying invoice: "+ $i );
		modify($a) {
			setBalance( $b - $q );
		};
		insert(new Paid($i));
end

rule "Transfer"
	when
		$t: Transfer( $pr : actor, $pe : payee, $am : amount )
		$ar : Account($br : balance, holder == $pr, minValue < $br - $am)
		$ae : Account($be : balance, holder == $pe)
	then
		modify($ar) {
			setBalance( $br - $am );
		};
		modify($ae) {
			setBalance( $be + $am );
		};
		insert( new Cleared($t) );
end
