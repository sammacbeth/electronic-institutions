import uk.ac.imperial.einst.micropay.Account
import uk.ac.imperial.einst.micropay.Invoice
import uk.ac.imperial.einst.micropay.Transfer

global org.apache.log4j.Logger logger

declare Paid
	invoice : Invoice
end

declare Cleared
	transfer : Transfer
end

declare Bounced
	transfer : Transfer
end

declare Transfer
	@role( event )
	@expires( 11s )
end

query cleared(Transfer t)
	Cleared(t := transfer)
end

query account(Object holder)
	account : Account(holder := holder)
end

rule "Pay Invoice"
	when
		$i : Invoice($p : payer, $q : amount)
		$a : Account(holder == $p, $b : balance, frozen == false)
		not( Paid($i;) )
	then
		logger.info("Paying invoice: "+ $i );
		modify($a) {
			setBalance( $b - $q );
		};
		insert(new Paid($i));
end

rule "Transfer"
	when
		$t: Transfer( $pr : actor, $pe : payee, $am : amount )
		not( Cleared($t;) or Bounced($t;) )
		$ar : Account($br : balance, holder == $pr, $min : minValue, frozen == false)
		$ae : Account($be : balance, holder == $pe, frozen == false)
	then
		if($min < $br - $am) {
			modify($ar) {
				setBalance( $br - $am );
			};
			modify($ae) {
				setBalance( $be + $am );
			};
			insert( new Cleared($t) );
		} else {
			insert( new Bounced($t) );
		}
end
